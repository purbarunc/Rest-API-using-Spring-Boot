plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "org.sonarqube" version "3.0"
	id 'jacoco'
}

group = 'com.codex'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.modelmapper:modelmapper:2.3.9'
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	implementation 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	doFirst {
		systemProperty 'spring.profiles.active', 'local'
	}
	useJUnitPlatform()
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
        xml.destination file("${rootProject.projectDir}/build/reports/jacoco/report.xml")
    }
}

sonarqube {
  properties {
    property "sonar.projectKey", "purbarunc_user-service"
    property "sonar.organization", "purbarunc"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.binaries", "build/classes"
    property "sonar.dynamicAnalysis", "reuseReports"
	property "sonar.java.coveragePlugin", "jacoco"
	property "sonar.coverage.jacoco.xmlReportPaths", "/build/reports/jacoco/report.xml"
  }
}
